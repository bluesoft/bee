buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'groovy'
    id 'eclipse'
}

apply plugin: 'application'

mainClassName = "br.com.bluesoft.bee.Bee"

group = 'br.com.bluesoft.bee'
def artifact = 'bee'
version = '1.109'

def javaVersion = JavaVersion.VERSION_1_8
sourceCompatibility = javaVersion;
targetCompatibility = javaVersion;

def startScriptsDir = new File("$buildDir/scripts")

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.22'
    implementation 'org.apache.groovy:groovy-sql:4.0.22'
    implementation 'org.apache.groovy:groovy-cli-commons:4.0.22'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.2'
    testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
    testImplementation 'org.spockframework:spock-junit4:2.3-groovy-4.0'
    testImplementation 'cglib:cglib-nodep:3.2.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.1"
    testRuntimeOnly 'org.slf4j:slf4j-api:1.7.10'
}

startScripts.doLast {
    def scriptsDir = new File("${startScriptsDir}")
    scriptsDir.eachFile {
        def script = it
        def scriptText = script.text
        scriptText = scriptText.replaceAll('CLASSPATH=.*', 'for i in \\$APP_HOME/lib/*;do CLASSPATH=\\$CLASSPATH:\\$i; done')
        script.write scriptText
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': "${artifact}", 'Implementation-Version': "${version}"
    }
}

applicationDistribution.from('bee.properties') {
    into 'bin'
}
